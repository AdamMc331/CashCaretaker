apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'com.google.gms.google-services'

androidExtensions {
    experimental = true
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]

    defaultConfig {
        applicationId "com.androidessence.cashcaretaker"
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        versionCode 13
        versionName "2.2.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(project(":database"))

    // Kotlin/Android
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    //noinspection GradleDependency
    implementation "com.google.android.material:material:1.1.0"
    annotationProcessor "androidx.databinding:databinding-compiler:$build_tools_version"
    kapt "androidx.databinding:databinding-compiler:$build_tools_version"

    // Third party
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.androidessence:materialdesignspecs:$materialDesignSpecsVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    kapt "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // ViewModel Kotlin support
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // Firebase
    implementation "com.google.firebase:firebase-analytics-ktx:17.4.4"

    // Koin for Kotlin apps
    implementation "org.koin:koin-core:2.1.6"
    implementation "org.koin:koin-android:2.1.6"
    implementation "org.koin:koin-android-viewmodel:2.1.6"
    // Testing
    testImplementation "org.koin:koin-test:2.1.6"

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation("androidx.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support'
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }

    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
}
repositories {
    mavenCentral()
}

ktlint {
    ktlint.version = "0.37.2"
    android = true
    enableExperimentalRules = true
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    additionalEditorconfigFile = file("${project.projectDir}/.editorConfig")
    outputColorName = "RED"
}

detekt {
    config = files("${rootProject.projectDir}/config/detekt/detekt.yml")

    reports {
        txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
    }
}
