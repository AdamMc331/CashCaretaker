// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.gitlab.arturbosch.detekt"

buildscript {
    ext.kotlin_version = "1.3.72"
    ext.build_tools_version = '4.2.0-alpha05'
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.29.0"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.3.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.10.0"
        classpath "com.google.gms:google-services:4.3.3"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "http://dl.bintray.com/androidessence/maven" }
    }

    ext {
        //Android
        appCompatVersion = "1.0.2"
        androidMinSdkVersion = 19
        androidTargetSdkVersion = 29
        androidCompileSdkVersion = 29
        lifecycleVersion = "2.0.0"
        coroutinesVersion = "1.3.8"
        architectureComponentsVersion = "2.1.0"
        materialVersion = "1.1.0-alpha06"
        espressoVersion = "3.2.0"

        //Libraries
        constraintLayoutVersion = "1.1.3"
        materialDesignSpecsVersion = "2.0.1"
        timberVersion = "4.7.1"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/**
 * This is a special task that allows us to pass a flag to avoid any tasks with Lint in the name.
 *
 * To use, you can call `./gradlew build -PnoLint` from the command line.
 *
 * https://kousenit.org/2016/04/20/excluding-gradle-tasks-with-a-name-pattern/
 */
gradle.taskGraph.whenReady { graph ->
    if (project.hasProperty('noLint')) {
        graph.allTasks.findAll { it.name ==~ /.*lint.*/ }*.enabled = false
    }
}